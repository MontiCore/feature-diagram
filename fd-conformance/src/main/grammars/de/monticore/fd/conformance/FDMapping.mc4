/* (c) https://github.com/MontiCore/monticore */
package de.monticore.fd.conformance;

/* This is a prototypical MontiCore grammar for defining incarnation mappings for Feature Diagrams.
 * Adaptations might not be conservative. */

grammar FDMapping extends  de.monticore.MCBasics {

  /**
   * An FDMapping encodes an incarnation mapping between a concrete Feature Diagram
   * and reference Feature Diagram.
   */
  symbol scope FDMapping = "mapping" Name "{"(FDMappingRule)* "}";

  /**
   * An FDMappingRule maps a compositions of elements of the concrete FD (on the left)
   * to an element of the reference FD (on the right).
   */
  FDMappingRule = leftSide:FDElementComposition "==>" rightSide:FDMapElement ";";

  /**
   * An FDElementComposition constitutes a composition of FDMapElements.
   * It is used to enable incarnation of reference features via compositions of concrete features
   */
  FDElementComposition  = FDMapElement ("++" FDMapElement)*;

  /**
   * An FDMapElement represents either a concrete or reference element.
   * Currently we consider only features which are referenced via name.
   */
  FDMapElement = Name ;

}
